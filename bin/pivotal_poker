#!/usr/bin/env coffee
##
# pivotal_poker [options]
#
# Launch the Pivotal Poker server
#

require.paths.unshift __dirname + '/../node_modules'
require.paths.unshift __dirname + '/../src'
require('zappa') ->
  @basedir = __dirname + '/../'
  @enable 'serve jquery'
  @use 'logger', static: @basedir + 'public'

  @get '/': ->
    @render 'index'

  @on login: ->
    @client.players ||= []
    @client.players.push @data.nickname
    @client.nickname = @data.nickname
    @emit login: {nickname: @client.nickname}
    @emit join: {nickname: @client.nickname}
    @broadcast join: {nickname: @client.nickname}

  @on logout: ->
    oldNickname = @client.nickname
    @client.nickname = null
    @emit 'logout'
    @emit leave: {nickname: oldNickname}
    @broadcast leave: {nickname: oldNickname}

  @on disconnect: ->
    @broadcast leave: {nickname: @client.nickname}

  @on said: ->
    if @client.nickname
      @broadcast said: {nickname: @client.nickname, text: @data.text}
      @emit said: {nickname: @client.nickname, text: @data.text}
    else
      @emit said: {error: 'Not Logged In'}

  @client '/js/index.js': ->
    @connect()

    @on said: ->
      if @data.error
        alert "Error: #{@data.error}"
      else
        $('#chat').append "<p>#{@data.nickname} said: #{@data.text}</p>"

    @on login: ->
      $('#user-info').html """
        Welcome #{@data.nickname}!
        <button id='logout' class='btn' type='submit'>Sign Out</button>
      """
    @on join: ->
      $('#players .row').append """
        <div class='span2' id='player-#{@data.nickname}' ><div>#{@data.nickname}</div></div>
      """

    @on logout: ->
      $('#user-info').html '''
        <form id='login' action:''>
          <input class='input-small' type='text' placeholder='Username'/>
          <button class='btn' type='submit'>Sign In</button>
        </form>
      '''

    @on leave: ->
      $("#players .row #player-#{@data.nickname}").remove()

    $ =>
      $('#logout').live 'click', (e) =>
        @emit 'logout'
        $('#chat-input button').attr disabled: true
        e.preventDefault()

      $('#login button').live 'click', (e) =>
        @emit login: {nickname: $('#login input').val()}
        $('#chat-input button').attr disabled: false
        e.preventDefault()

      $('#chat-input button').click (e) =>
        @emit said: {text: $('#box').val()}
        $('#box').val('').focus()
        e.preventDefault()

  @view layout: ->
    doctype 5
    html ->
      head ->
        title @title or 'Pivotal Poker'
        script src: '/socket.io/socket.io.js'
        script src: '/zappa/jquery.js'
        script src: '/zappa/zappa.js'
        script src: '/js/index.js'

        link rel: 'stylesheet', href: 'http://twitter.github.com/bootstrap/1.3.0/bootstrap.min.css'
        link rel: 'stylesheet', href: '/css/site.css'
      body -> 
        div class: 'topbar', ->
          div class: 'fill', ->
            div class: 'container', ->
              a class: 'brand', href: '/', -> @title or 'Pivotal Poker'

              ul class: 'nav', ->
                li class:'active', ->
                  a href:'#', -> 'Home'
                li  ->
                  a href:'#', -> 'About'
                li  ->
                  a href:'#', -> 'Contact'
              div id: 'user-info', class: 'pull-right', ->
                form id:'login',  action:'', ->
                  input class:'input-small', type:'text', placeholder:'Username'
                  button class:'btn', type:'submit', -> 'Sign in'

        div class: 'container', ->
          div class: 'content', -> @body

  @view index: ->
    div class: 'page-header', ->
      h1 -> 'Pivotal Poker'
    div class: 'row', ->
      div class: 'span10', ->
        div class: 'story', ->
          h2 "Story: Reconfigure apache on all boxes with redis logging"
        div id: 'players', class: 'players', ->
          div class: 'row', '&nbsp;'
      div class: 'span4', ->
        h3 -> 'Chat'
        div id: 'chat'
        form id: 'chat-input', class: 'pull-right', ->
          input class: 'span3', type: 'text', id: 'box'
          button class:'btn', disabled: true, 'Send'

# vim:ft=coffee ts=2 sw=2 et :
