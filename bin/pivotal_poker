#!/usr/bin/env coffee
##
# pivotal_poker [options]
#
# Launch the Pivotal Poker server
#

require.paths.unshift __dirname + '/../node_modules'
require.paths.unshift __dirname + '/../src'
require('zappa') ->
  @basedir = __dirname + '/../'
  @enable 'serve jquery'
  @use 'logger', static: @basedir + 'public'

  @get '/': ->
    @render 'index'

  @on 'set nickname': ->
    @client.nickname = @data.nickname

  @on said: ->
    @broadcast said: {nickname: @client.nickname, text: @data.text}
    @emit said: {nickname: @client.nickname, text: @data.text}

  @client '/js/index.js': ->
    @connect()

    @on said: ->
      $('#chat').append "<p>#{@data.nickname} said: #{@data.text}</p>"

    $ =>
      @emit 'set nickname': {nickname: prompt 'Pick a nickname!'}

      $('#box').focus()

      $('button').click (e) =>
        @emit said: {text: $('#box').val()}
        $('#box').val('').focus()
        e.preventDefault()

  @view layout: ->
    doctype 5
    html ->
      head ->
        title 'Pivotal Poker'
        script src: '/socket.io/socket.io.js'
        script src: '/zappa/jquery.js'
        script src: '/zappa/zappa.js'
        script src: '/js/index.js'

        link rel: 'stylesheet', href: 'http://twitter.github.com/bootstrap/1.3.0/bootstrap.min.css'
        link rel: 'stylesheet', href: '/css/site.css'
      body -> 
        div class: 'topbar', ->
          div class: 'fill', ->
            div class: 'container', ->
              a class: 'brand', href: '/', -> 'Home'

              ul class: 'nav', ->
                li class:'active', ->
                  a href:'#', -> 'Home'
                li  ->
                  a href:'#', -> 'About'
                li  ->
                  a href:'#', -> 'Contact'
              form action:'', class:'pull-right', ->
                input class:'input-small', type:'text', placeholder:'Username'
                input class:'input-small', type:'password', placeholder:'Password'
                button class:'btn', type:'submit', -> 'Sign in'

        div class: 'container', ->
          div class: 'content', -> @body

  @view index: ->
    div class: 'page-header', ->
      h1 -> 'Page name'
    div class: 'row', ->
      div class: 'span10', ->
        h2 -> 'Primary content'
      div class: 'span4', ->
        h3 -> 'Chat'
        div id: 'chat'
        form id: 'chat-input', class: 'pull-right', ->
          input class: 'span3', type: 'text', id: 'box'
          button class:'btn', 'Send'

# vim:ft=coffee ts=2 sw=2 et :
